// stats - show current player stats of specified (or all) person(s)
// Author: /BudSpencer ( 2016 )
// supports totalstats system too

str = ""
msg = ""

getplayerstatstr = [
	str = ( format "Stats for ^f3%1(%2)^f7: Frags: ^f4%3 ^f7/ Deaths: ^f4%4 ^f7/ Accuracy: ^f4%5%% ^f7/ KpD: ^f4%6 ^f7/ Suicides: ^f4%7" ( getname $arg1 ) ( getcn $arg1 ) ( getfrags $arg1 ) ( getdeaths $arg1 ) ( getaccuracy $arg1 ) ( getkpd $arg1 ) ( getsuicides $arg1 ) )	
	if ( isteammode ) [
		str = ( concatword $str ( format " ^f7/ Teamkills: ^f4%1" ( getteamkills $arg1 ) ) )
		
		if ( isflagmode ) [
			str = ( concatword $str ( format " ^f7/ Flagruns: ^f4%1" ( getflags $arg1 ) ) )
		]
	]
	str = ( concatword $str ( format " ^f7/ Damage: ^f4%1" ( getdamage $arg1 ) ) )
	str = ( concatword $str ( format " ^f7/ Rank: ^f4%1" ( getrank $arg1 ) ) ) 
	str = ( concatword $str ( format " ^f7/ Time: ^f4%1" ( formatmillis "%hh %mm %ss" ( getonline $arg1 ) ) ) )
	str = ( concatword $str ( format "^n^f7Weapon ^f3Accuracies^f7: ^f7Shotgun[SG]: ^f4%1%% / ^f7Minigun(Chaingun)[CG]: ^f4%2%% ^f7/ ^f7Rocket Launcher[RL]: ^f4%3%% ^f7/ Rifle[RI]: ^f4%4%% ^f7/Grenade Launcher[GL]: ^f4%5%% ^f7/ Pistol[PI]: ^f4%6%%" ( getwepaccuracy $arg1 1 ) ( getwepaccuracy $arg1 2 ) ( getwepaccuracy $arg1 3 ) ( getwepaccuracy $arg1 4 ) ( getwepaccuracy $arg1 5 ) ( getwepaccuracy $arg1 6 ) ) )
	result $str
]

getplayerstatstr_total = [
	str = ( format "Total Stats for ^f3%1(%2)^f7: Frags: ^f4%3 ^f7/ Deaths: ^f4%4 ^f7/ Accuracy: ^f4%5%% ^f7/ KpD: ^f4%6 ^f7/ Suicides: ^f4%7" ( getname $arg1 ) ( getcn $arg1 ) ( gettotalfrags $arg1 ) ( gettotaldeaths $arg1 ) ( gettotalaccuracy $arg1 ) ( gettotalkpd $arg1 ) ( gettotalsuicides $arg1 ) )	
	str = ( concatword $str ( format " ^f7/ Teamkills: ^f4%1" ( gettotalteamkills $arg1 ) ) )
	str = ( concatword $str ( format " ^f7/ Flagruns: ^f4%1" ( gettotalflags $arg1 ) ) )
	str = ( concatword $str ( format " ^f7/ Damage: ^f4%1" ( gettotaldamage_ $arg1 ) ) )
	str = ( concatword $str ( format " ^f7/ Games: ^f4%1" ( gettotalgames $arg1 ) ) ) 
	result $str
]

cmd_stats = [
	if ( ! $arg3 ) [
		if ( =s $arg2 "" ) [
			msg = ( getplayerstatstr $arg1 )
		] [ 
		if ( =s $arg2 "-1" ) [ 
			looplist cn ( allplayers ) [
				msg = ( concat $msg "^n" )
				msg = ( concat $msg ( getplayerstatstr $cn ) )
			] 
		] [ 
		msg = ( getplayerstatstr $arg2 )
			]
		]
	] [
	if ( =s $arg3 "TOTAL" ) [
		msg = ( getplayerstatstr_total $arg2 )
		]
	]
	
	pm $arg1 $msg
]
	
		

registercommand "stats" cmd_stats 1 "|iw" "stats <cn|-1> (<TOTAL>) ^f3That command allows you to get your or other player stats. You can type #stats -1 to get stats of all players in a row. To get total stats type #stats <cn> TOTAL."
			
			
	